apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

android {
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir '../protos'
            }
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "edu.cmu.cs.openscout"
        targetSdkVersion 28
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            isDefault = true
            minSdkVersion 24
        }
        timing {
            minSdkVersion 24
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'edu.cmu.cs.gabriel:client:2.0.1'

    implementation 'edu.cmu.cs.gabriel:protocol:2.0.1'
}
